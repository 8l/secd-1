        (LETREC COMPILE
            (COMPILE LAMBDA (E)
                 (COMP E (QUOTE NIL) (QUOTE (4 21))))
        (COMP LAMBDA (E N C)
           (IF (NUMBER E)
                (CONS 2 (CONS E C))
           (IF (ATOM E)
                (CONS 1 (CONS (LOCATION E N) C))
           (IF (EQ (CAR E) (QUOTE QUOTE))
                (CONS 2 (CONS (CAR (CDR E)) C))
           (IF (EQ (CAR E) (QUOTE ADD))
                (COMP (CAR (CDR E)) N (COMP (CAR (CDR (CDR E))) N (CONS 15 C)))
           (IF (EQ (CAR E) (QUOTE SUB))
                (COMP (CAR (CDR E)) N (COMP (CAR (CDR (CDR E))) N (CONS 16 C)))
           (IF (EQ (CAR E) (QUOTE MUL))
                (COMP (CAR (CDR E)) N (COMP (CAR (CDR (CDR E))) N (CONS 17 C)))
           (IF (EQ (CAR E) (QUOTE DIV))
                (COMP (CAR (CDR E)) N (COMP (CAR (CDR (CDR E))) N (CONS 18 C)))
           (IF (EQ (CAR E) (QUOTE REM))
                (COMP (CAR (CDR E)) N (COMP (CAR (CDR (CDR E))) N (CONS 19 C)))
           (IF (EQ (CAR E) (QUOTE LEQ))
                (COMP (CAR (CDR E)) N (COMP (CAR (CDR (CDR E))) N (CONS 20 C)))
           (IF (EQ (CAR E) (QUOTE EQ))
                (COMP (CAR (CDR E)) N (COMP (CAR (CDR (CDR E))) N (CONS 14 C)))
           (IF (EQ (CAR E) (QUOTE CAR))
                (COMP (CAR (CDR E)) N (CONS 10 C))
           (IF (EQ (CAR E) (QUOTE CDR))
                (COMP (CAR (CDR E)) N (CONS 11 C))
           (IF (EQ (CAR E) (QUOTE ATOM))
                (COMP (CAR (CDR E)) N (CONS 12 C))
           (IF (EQ (CAR E) (QUOTE SYMBOL))
                (COMP (CAR (CDR E)) N (CONS 22 C))
           (IF (EQ (CAR E) (QUOTE NUMBER))
                (COMP (CAR (CDR E)) N (CONS 23 C))
           (IF (EQ (CAR E) (QUOTE GET))
                (CONS 24 C)
           (IF (EQ (CAR E) (QUOTE PUT))
                (COMP (CAR (CDR E)) N (CONS 25 C)) 
           (IF (EQ (CAR E) (QUOTE CONS))
                (COMP (CAR (CDR (CDR E))) N (COMP (CAR (CDR E)) N (CONS 13 C)))
           (IF (EQ (CAR E) (QUOTE IF))
                (LET (COMP (CAR (CDR E)) N (CONS 8
                                            (CONS THENPT (CONS ELSEPT C))))
                (THENPT COMP (CAR (CDR (CDR E))) N (QUOTE (9)))
                (ELSEPT COMP (CAR (CDR (CDR (CDR E)))) N (QUOTE (9)))  )
           (IF (EQ (CAR E) (QUOTE LAMBDA))
                (LET (CONS 3 (CONS BODY C))
                   (BODY COMP (CAR (CDR (CDR E))) (CONS (CAR (CDR E)) N)
                                        (QUOTE (5))) )
           (IF (EQ (CAR E) (QUOTE LET))
        (LET (LET (COMPLIS ARGS N (CONS 3
                         (CONS BODY (CONS 4 C))))
                (BODY COMP (CAR (CDR E)) M (QUOTE (5))))
           (M CONS (VARS (CDR (CDR E))) N)
                (ARGS EXPRS (CDR (CDR E))))
           (IF (EQ (CAR E) (QUOTE LETREC))
                (LET (LET (CONS 6 (COMPLIS ARGS M       
                        (CONS 3 (CONS BODY (CONS 7 C)))))
                (BODY COMP (CAR (CDR E)) M (QUOTE (5))))
             (M CONS (VARS (CDR (CDR E))) N)
             (ARGS EXPRS (CDR (CDR E))))
        (COMPLIS (CDR E) N (COMP (CAR E) N (CONS 4 C))))))))))))))))))))))))))
        (COMPLIS LAMBDA (E N C)
           (IF (EQ E (QUOTE NIL)) (CONS 2 (CONS (QUOTE NIL) C))
                (COMPLIS (CDR E) N (COMP (CAR E) N (CONS 13 C)))))
        (LOCATION LAMBDA (E N)
           (LETREC
                (IF (MEMBER E(CAR N)) (CONS 0 (POSN E (CAR N)))
                        (INCAR (LOCATION E (CDR N))))
           (MEMBER LAMBDA (E N)
                (IF (EQ N (QUOTE NIL)) (QUOTE NIL)
                (IF (EQ E (CAR N)) (QUOTE T) (MEMBER E (CDR N)))))
           (POSN LAMBDA (E N)
            (IF (EQ E (CAR N)) 0 (ADD 1 (POSN E (CDR N)))))
           (INCAR LAMBDA (L) (CONS (ADD 1 (CAR L)) (CDR L)))))
        (VARS LAMBDA (D)
           (IF (EQ D (QUOTE NIL)) (QUOTE NIL)
                (CONS (CAR (CAR D)) (VARS (CDR D)))))
        (EXPRS LAMBDA (D)
           (IF (EQ D (QUOTE NIL)) (QUOTE NIL)
                (CONS (CDR (CAR D)) (EXPRS (CDR D))))) )
