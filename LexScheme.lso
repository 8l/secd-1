  (LEX LAMBDA (CC)
    (LEXINITIAL (SCAN) CC))

  (LEXINITIAL LAMBDA (C CC)
    (IF (LEQ C 0)
        (YIELDEOF CC)
    (IF (ISOPENPAREN C)
        (YIELD (QUOTE (OPENP)) CC)
    (IF (ISCLOSEPAREN C)
        (YIELD (QUOTE (CLOSEP)) CC)
    (IF (ISINITIAL C)
        (LEXIDENT C CC)
    (IF (ISDOT C)
        (LEXSTARTSWITHDOT (GET) CC)
    (IF (ISPOUND C)
        (LEXSTARTSWITHPOUND (GET) CC)
    (IF (ISDQUOTE C)
        (LEXSTRING (GET) CC)
    (IF (ISQUOTE C)
        (YIELD (QUOTE (QUOTE)) CC)
    (IF (ISBACKQUOTE C)
        (YIELD (QUOTE (BACKQUOTE)) CC)
    (IF (ISCOMMA C)
        (LEXSTARTSWITHCOMMA (GET) CC)
    (IF (ISSEMI C)
        (LEXINITIAL (FIRST (SCAN) (SKIPCOMMENT)) CC)
    (IF (ISWHITESPACE C)
        (LEXINITIAL (SCAN) CC)
    (IF (ISDIGIT C)
        (LEXCOMPLEX C 10 (QUOTE NIL) DIGITVALUEDEC CC)
    (IF (ISPLUS C)
        (LEXSTARTSWITHSIGN (GET) (QUOTE (PLUS)) CC)
    (IF (ISMINUS C)
        (LEXSTARTSWITHSIGN (GET) (QUOTE (MINUS)) CC)
        (YIELDERROR CC)))))))))))))))))

  (YIELD LAMBDA (TOKEN CC)
    (CC TOKEN (LAMBDA (CC) (LEXINITIAL (SCAN) CC))))

  (YIELDC LAMBDA (TOKEN C CC)
    (CC TOKEN (LAMBDA (CC) (LEXENDTOKEN C CC))))

  (YIELDEOF LAMBDA (CC)
    (CC (QUOTE NIL) (LAMBDA (CC) (YIELDEOF CC))))

  (YIELDERROR LAMBDA (CC)
    (CC (QUOTE (ERROR)) (LAMBDA (CC) (YIELDEOF CC))))

  (LEXSTARTSWITHSIGN LAMBDA (C TOKEN CC)
    (IF (OR (ISDELIM C) (ISWHITESPACE C))
        (YIELDC TOKEN C CC)
        (LEXCOMPLEX4 C 10 (QUOTE NIL) DIGITVALUEDEC CC)))

  (LEXIDENT LAMBDA (C CC)
    (CONTINUELEXIDENT (GET) (CONS C (QUOTE NIL)) CC))

  (CONTINUELEXIDENT LAMBDA (C I CC)
    (IF (ISSUBSEQUENT C)
        (CONTINUELEXIDENT (GET) (CONS C I) CC)
        (LET
          (LET
            (IF (ISNIL KEYWORD)
                (YIELDC (CONS (QUOTE IDENT) NAME) C CC)
                (YIELDC (CONS KEYWORD (QUOTE NIL)) C CC))
            (KEYWORD GETKEYWORD NAME))
          (NAME REVERSE I))))

  (LEXSTARTSWITHDOT LAMBDA (C CC)
    (IF (ISDOT C)
        (LEXELLIPSIS (GET) CC)
    (IF (ISDIGIT C)
        (LEXREAL7 C 10 (QUOTE NIL) DIGITVALUEDEC LEXCOMPLEX1 CC)
        (YIELDC (QUOTE (DOT)) C CC))))

  (LEXSTARTSWITHCOMMA LAMBDA (C CC)
    (IF (ISATSIGN C)
        (YIELD (QUOTE (COMMAAT)) CC)
        (CC (QUOTE (COMMA)) (LAMBDA (CC) (LEXINITIAL C CC)))))

  (LEXELLIPSIS LAMBDA (C CC)
    (IF (ISDOT C)
        (CC (QUOTE (IDENT ELLIPSIS)) (LAMBDA (CC) (LEXENDTOKEN (GET) CC)))
        (YIELDERROR CC)))
  
  (LEXENDTOKEN LAMBDA (C CC)
    (IF (LEQ C 0)
        (YIELDEOF CC)
    (IF (ISWHITESPACE C)
        (LEXINITIAL (SCAN) CC)
    (IF (ISDELIM C)
        (LEXINITIAL C CC)
        (YIELDERROR CC)))))

  (LEXSTARTSWITHPOUND LAMBDA (C CC)
    (IF (IST C)
        (CC (QUOTE (BOOL T)) (LAMBDA (CC) (LEXENDTOKEN (GET) CC)))
    (IF (ISF C)
        (CC (QUOTE (BOOL F)) (LAMBDA (CC) (LEXENDTOKEN (GET) CC)))
    (IF (ISOPENPAREN C)
        (YIELD (QUOTE (OPENVEC)) CC)
    (IF (ISBACKSLASH C)
        (LEXCHARACTER (GET) CC)
    (IF (ISNUMBERPREFIX C)
        (LEXNUMBERPREFIXITEM C (QUOTE NIL) (QUOTE NIL) CC)
        (YIELDERROR CC)))))))

  (LEXNUMBERPREFIX LAMBDA (C BASE EXACT CC)
    (IF (ISPOUND C)
        (LEXNUMBERPREFIX (GET) BASE EXACT CC)
        (LEXNUMBER C BASE EXACT CC)))

  (LEXNUMBER LAMBDA (C BASE EXACT CC)
    (LET
      (LET
        (LEXCOMPLEX C BASE EXACT DIGITVALUE CC)
        (DIGITVALUE . (IF (EQ BASE 2) DIGITVALUEBIN
                      (IF (EQ BASE 8) DIGITVALUEOCT
                      (IF (EQ BASE 10) DIGITVALUEDEC
                      (IF (EQ BASE 16) DIGITVALUEHEX
                          (LAMBDA (C) (QUOTE -1))))))))
      (EXACT IF (ISNIL EXACT) (QUOTE T) EXACT)
      (BASE IF (ISNIL BASE) 10 BASE)))

  (LEXCOMPLEX LAMBDA (C BASE EXACT DIGITVALUE CC)
    (IF (ISSIGN C)
        (LEXCOMPLEX4 (GET) BASE EXACT DIGITVALUE CC)
        (LEXUREAL C BASE EXACT DIGITVALUE LEXCOMPLEX1 CC)))

  (LEXCOMPLEX1 LAMBDA (C BASE EXACT DIGITVALUE CC)
    (IF (ISSIGN C)
        (LEXCOMPLEX7 (GET) BASE EXACT DIGITVALUE CC)
    (IF (ISATSIGN C)
        (LEXCOMPLEX2 (GET) BASE EXACT DIGITVALUE CC)
        (YIELDC (QUOTE (NUMBER)) C CC))))

  (LEXCOMPLEX2 LAMBDA (C BASE EXACT DIGITVALUE CC)
    (LEXREAL C BASE EXACT DIGITVALUE (QUOTE NIL) LEXCOMPLEX3 CC))

  (LEXCOMPLEX3 LAMBDA (C BASE EXACT DIGITVALUE CC)
    (YIELDC (QUOTE (NUMBER)) C CC))

  (LEXCOMPLEX4 LAMBDA (C BASE EXACT DIGITVALUE CC)
    (IF (ISIMAGUNIT C)
        (LEXCOMPLEX5 (GET) BASE EXACT DIGITVALUE CC)
        (LEXUREAL C BASE EXACT DIGITVALUE LEXCOMPLEX6 CC)))

  (LEXCOMPLEX5 LAMBDA (C BASE EXACT DIGITVALUE CC)
    (YIELDC (QUOTE (NUMBER)) C CC))

  (LEXCOMPLEX6 LAMBDA (C BASE EXACT DIGITVALUE CC)
    (IF (ISSIGN C)
        (LEXCOMPLEX7 (GET) BASE EXACT DIGITVALUE CC)
    (IF (ISATSIGN C)
        (LEXCOMPLEX2 (GET) BASE EXACT DIGITVALUE CC)
    (IF (ISIMAGUNIT C)
        (LEXCOMPLEX5 (GET) BASE EXACT DIGITVALUE CC)
        (YIELDC (QUOTE (NUMBER)) C CC)))))

  (LEXCOMPLEX7 LAMBDA (C BASE EXACT DIGITVALUE CC)
    (IF (ISIMAGUNIT C)
        (LEXCOMPLEX5 (GET) BASE EXACT DIGITVALUE CC)
        (LEXUREAL C BASE EXACT DIGITVALUE LEXCOMPLEX8 CC)))

  (LEXCOMPLEX8 LAMBDA (C BASE EXACT DIGITVALUE CC)
    (IF (ISIMAGUNIT C)
        (LEXCOMPLEX5 (GET) BASE EXACT DIGITVALUE CC)
        (YIELDERROR CC)))

  (LEXUREAL LAMBDA (C BASE EXACT DIGITVALUE TERMINAL CC)
    (LEXREAL C BASE EXACT DIGITVALUE 43 TERMINAL CC))

  (LEXREAL LAMBDA (C BASE EXACT DIGITVALUE SIGN TERMINAL CC)
    (IF (ISSIGN C)
        (IF (ISNIL SIGN)
            (LEXREAL1 (GET) BASE EXACT DIGITVALUE C TERMINAL CC)
            (YIELDERROR CC))
    (IF (ISDOT C)
        (IF (EQ BASE 10)
            (LEXREAL7 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
            (YIELDERROR CC))
        (LET
          (IF (GEQ DIGIT 0)
              (LEXREAL2 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
              (YIELDERROR CC))
          (DIGIT DIGITVALUE C)))))

  (LEXREAL1 LAMBDA (C BASE EXACT DIGITVALUE SIGN TERMINAL CC)
    (IF (ISDOT C)
        (IF (EQ BASE 10)
            (LEXREAL7 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
            (YIELDERROR CC))
        (LET
          (IF (GEQ DIGIT 0)
              (LEXREAL2 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
              (YIELDERROR CC))
          (DIGIT DIGITVALUE C))))

  (LEXREAL2 LAMBDA (C BASE EXACT DIGITVALUE SIGN TERMINAL CC)
    (IF (ISDOT C)
        (IF (EQ BASE 10)
            (LEXREAL8 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
            (YIELDERROR CC))
    (IF (ISPOUND C)
        (LEXREAL3 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
        (LET
          (IF (GEQ DIGIT 0)
              (LEXREAL2 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
              (TERMINAL C BASE EXACT DIGITVALUE CC))
          (DIGIT DIGITVALUE C)))))

  (LEXREAL3 LAMBDA (C BASE EXACT DIGITVALUE SIGN TERMINAL CC)
    (IF (ISDOT C)
        (IF (EQ BASE 10)
            (LEXREAL9 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
            (YIELDERROR CC))
    (IF (ISPOUND C)
        (LEXREAL3 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
    (IF (ISSLASH C)
        (LEXREAL4 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
        (TERMINAL C BASE EXACT DIGITVALUE CC)))))

  (LEXREAL4 LAMBDA (C BASE EXACT DIGITVALUE SIGN TERMINAL CC)
    (LET
      (IF (GEQ DIGIT 0)
          (LEXREAL5 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
          (YIELDERROR CC))
      (DIGIT DIGITVALUE C)))

  (LEXREAL5 LAMBDA (C BASE EXACT DIGITVALUE SIGN TERMINAL CC)
    (IF (ISPOUND C)
        (LEXREAL6 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
        (LET
          (IF (GEQ DIGIT 0)
              (LEXREAL5 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
              (TERMINAL C BASE EXACT DIGITVALUE CC))
          (DIGIT DIGITVALUE C))))

  (LEXREAL6 LAMBDA (C BASE EXACT DIGITVALUE SIGN TERMINAL CC)
    (IF (ISPOUND C)
        (LEXREAL6 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
        (TERMINAL C BASE EXACT DIGITVALUE CC)))

  (LEXREAL7 LAMBDA (C BASE EXACT DIGITVALUE SIGN TERMINAL CC)
    (LET
      (IF (GEQ DIGIT 0)
          (LEXREAL8 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
          (YIELDERROR CC))
      (DIGIT DIGITVALUE C)))

  (LEXREAL8 LAMBDA (C BASE EXACT DIGITVALUE SIGN TERMINAL CC)
    (IF (ISPOUND C)
        (LEXREAL9 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
    (IF (ISEXPONENTMARKER C)
        (LEXREAL10 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
        (LET
          (IF (GEQ DIGIT 0)
              (LEXREAL8 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
              (TERMINAL C BASE EXACT DIGITVALUE CC))
          (DIGIT DIGITVALUE C)))))

  (LEXREAL9 LAMBDA (C BASE EXACT DIGITVALUE SIGN TERMINAL CC)
    (IF (ISPOUND C)
        (LEXREAL9 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
    (IF (ISEXPONENTMARKER C)
        (LEXREAL10 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
        (TERMINAL C BASE EXACT DIGITVALUE CC))))

  (LEXREAL10 LAMBDA (C BASE EXACT DIGITVALUE SIGN TERMINAL CC)
    (IF (ISSIGN C)
        (LEXREAL11 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
        (LET
          (IF (GEQ DIGIT 0)
              (LEXREAL12 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
              (YIELDERROR CC))
          (DIGIT DIGITVALUE C))))

  (LEXREAL11 LAMBDA (C BASE EXACT DIGITVALUE SIGN TERMINAL CC)
    (LET
      (IF (GEQ DIGIT 0)
          (LEXREAL12 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
          (YIELDERROR CC))
      (DIGIT DIGITVALUE C)))

  (LEXREAL12 LAMBDA (C BASE EXACT DIGITVALUE SIGN TERMINAL CC)
    (LET
      (IF (GEQ DIGIT 0)
          (LEXREAL12 (GET) BASE EXACT DIGITVALUE SIGN TERMINAL CC)
          (TERMINAL C BASE EXACT DIGITVALUE CC))
      (DIGIT DIGITVALUE C)))

  (LEXNUMBERPREFIXITEM LAMBDA (C BASE EXACT CC)
    (IF (ISB C)
        (IF (ISNIL BASE)
            (LEXNUMBERPREFIX (GET) 2 EXACT CC)
            (YIELDERROR CC))
    (IF (ISO C)
        (IF (ISNIL BASE)
            (LEXNUMBERPREFIX (GET) 8 EXACT CC)
            (YIELDERROR CC))
    (IF (ISD C)
        (IF (ISNIL BASE)
            (LEXNUMBERPREFIX (GET) 10 EXACT CC)
            (YIELDERROR CC))
    (IF (ISX C)
        (IF (ISNIL BASE)
            (LEXNUMBERPREFIX (GET) 16 EXACT CC)
            (YIELDERROR CC))
    (IF (ISI C)
        (IF (ISNIL EXACT)
            (LEXNUMBERPREFIX (GET) BASE (QUOTE F) CC)
            (YIELDERROR CC))
    (IF (ISE C)
        (IF (ISNIL EXACT)
            (LEXNUMBERPREFIX (GET) BASE (QUOTE T) CC)
            (YIELDERROR CC))
        (YIELDERROR CC))))))))

  (LEXCHARACTER LAMBDA (C CC)
    (IF (LEQ C 0)
        (YIELDERROR CC)
    (IF (ISN C)
        (LEXCHARACTERSTARTSWITHN (GET) CC)
    (IF (ISS C)
        (LEXCHARACTERSTARTSWITHS (GET) CC)
        (YIELDC (CONS (QUOTE CHAR) (CONS C (QUOTE NIL))) (GET) CC)))))

  (LEXCHARACTERSTARTSWITHN LAMBDA (C CC)
    (IF (ISE C)
        (IF (EXPECT (QUOTE (119 108 105 110 101)))
            (YIELDC (QUOTE (CHAR 10)) (GET) CC)
            (YIELDERROR CC))
        (YIELDC (QUOTE (CHAR 110)) C CC)))
 
  (LEXCHARACTERSTARTSWITHS LAMBDA (C CC)
    (IF (ISP C)
        (IF (EXPECT (QUOTE (97 99 101)))
            (YIELDC (QUOTE (CHAR 32)) (GET) CC)
            (YIELDERROR CC))
        (YIELDC (QUOTE (CHAR 115)) C CC)))

  (EXPECT LAMBDA (S)
    (IF (ISNIL S)
        (QUOTE T)
        (IF (EQ (GET) (CAR S))
            (EXPECT (CDR S))
            (QUOTE F))))
        
  (LEXSTRING LAMBDA (C CC)
    (LEXSTRINGELEM C (QUOTE NIL) CC))

  (LEXSTRINGELEM LAMBDA (C S CC)
    (IF (LEQ C 0)
        (YIELDERROR CC)
    (IF (ISDQUOTE C)
        (YIELD (CONS (QUOTE STRING) (REVERSE S)) CC)
    (IF (ISBACKSLASH C)
        (LEXSTRINGESC (GET) S CC)
        (LEXSTRINGELEM (GET) (CONS C S) CC)))))

  (LEXSTRINGESC LAMBDA (C S CC)
    (IF (LEQ C 0)
        (YIELDERROR CC)
    (IF (ISBACKSLASH C)
        (LEXSTRINGELEM (GET) (CONS C S) CC)
    (IF (ISDQUOTE C)
        (LEXSTRINGELEM (GET) (CONS C S) CC)
        (YIELDERROR CC)))))

  (GETKEYWORD LAMBDA (S)
    (CONTINUEGETKEYWORD S KEYWORDS))

  (CONTINUEGETKEYWORD LAMBDA (S K)
    (IF (ISNIL K)
        (QUOTE NIL)
        (IF (ISNIL S)
            (IF (SYMBOL (CAR K))
                (CAR K)
                (QUOTE NIL))
            (CONTINUEGETKEYWORD (CDR S) (TRIECHILD (CAR S) K)))))

  (TRIECHILD LAMBDA (C K)
    (IF (ISNIL K)
        K
        (IF (ISCONS (CAR K))
            (IF (EQ C (CAR (CAR K)))
                (CDR (CAR K))
                (TRIECHILD C (CDR K)))
            (TRIECHILD C (CDR K)))))

  (KEYWORDS QUOTE (
    (61  (62  ARROW))
    (97  (110 (100 AND)))
    (98  (101 (103 (105 (110 BEGIN)))))
    (99  (97  (115 (101 CASE)))
         (111 (110 (100 COND))))
    (100 (101 (102 (105 (110 (101 DEFINE))))
              (108 (97  (121 DELAY))))
         (111 DO))
    (101 (108 (115 (101 ELSE))))
    (105 (102 IF))
    (108 (97  (109 (98  (100 (97  LAMBDA)))))
         (101 (116 LET
                   (42  LETSTAR)
                   (114 (101 (99  LETREC))))))
    (111 (114 OR))
    (113 (117 (97  (115 (105 (113 (117 (111 (116 (101 QUASIQUOTE))))))))
              (111 (116 (101 QUOTE)))))
    (115 (101 (116 (33  SETBANG))))
    (117 (110 (113 (117 (111 (116 (101 UNQUOTE
                                       (45  (115 (112 (108 (105 (99  (105 (110 (103 UNQUOTESPLICING))))))))))))))))
      ))

  (ISCONS LAMBDA (E) (IF (ATOM E) (QUOTE F) (QUOTE T)))
  (GEQ LAMBDA (A B) (LEQ B A))
  (AND LAMBDA (P Q) (IF P Q (QUOTE F)))
  (OR LAMBDA (P Q) (IF P (QUOTE T) Q))
  (IST LAMBDA (C) (OR (EQ C 116) (EQ C 84)))
  (ISF LAMBDA (C) (OR (EQ C 102) (EQ C 70)))
  (ISB LAMBDA (C) (OR (EQ C 98) (EQ C 66)))
  (ISO LAMBDA (C) (OR (EQ C 111) (EQ C 79)))
  (ISD LAMBDA (C) (OR (EQ C 100) (EQ C 68)))
  (ISX LAMBDA (C) (OR (EQ C 120) (EQ C 88)))
  (ISE LAMBDA (C) (OR (EQ C 101) (EQ C 69)))
  (ISI LAMBDA (C) (OR (EQ C 105) (EQ C 73)))
  (ISN LAMBDA (C) (OR (EQ C 110) (EQ C 78)))
  (ISS LAMBDA (C) (OR (EQ C 115) (EQ C 83)))
  (ISP LAMBDA (C) (OR (EQ C 112) (EQ C 80)))
  (ISNUMBERPREFIX LAMBDA (C)
    (CONTAINS (TOLOWER C) (QUOTE (98 111 100 120 105 101))))
  (ISNIL LAMBDA (L) (EQ L (QUOTE NIL)))
  (REVERSE LAMBDA (L)
    (CONTINUEREVERSE L (QUOTE NIL)))
  (CONTINUEREVERSE LAMBDA (L A)
    (IF (ISNIL L)
        A
        (CONTINUEREVERSE (CDR L) (CONS (CAR L) A))))
  (ISINRANGE LAMBDA (X MIN MAX)
    (AND (LEQ MIN X) (LEQ X MAX)))
  (ISDIGIT LAMBDA (C) (ISINRANGE C 48 57))
  (ISNONZERODIGIT LAMBDA (C) (ISINRANGE C 49 57))
  (CONTAINS LAMBDA (E L)
    (IF (ISNIL L)
        (QUOTE F)
        (OR (EQ E (CAR L)) (CONTAINS E (CDR L)))))
  (ISUPPER LAMBDA (C)
    (ISINRANGE C 65 90))
  (ISLOWER LAMBDA (C)
    (ISINRANGE C 97 122))
  (TOLOWER LAMBDA (C)
    (IF (ISUPPER C) (ADD C 32) C))
  (ISLETTER LAMBDA (C)
    (OR (ISUPPER C) (ISLOWER C)))
  (ISWHITESPACE LAMBDA (C)
    (CONTAINS C (QUOTE (32 9 10 13 0))))
  (ISDOT LAMBDA (C) (EQ C 46))
  (ISPOUND LAMBDA (C) (EQ C 35))
  (ISQUOTE LAMBDA (C) (EQ C 39))
  (ISOPENPAREN LAMBDA (C) (EQ C 40))
  (ISCLOSEPAREN LAMBDA (C) (EQ C 41))
  (ISNEWLINE LAMBDA (C) (EQ C 10))
  (ISSEMI LAMBDA (C) (EQ C 59))
  (ISSPECIALINITIAL LAMBDA (C)
    (CONTAINS C
      (QUOTE (33 36 37 38 42 47 58 60 61 62 63 126 95 94))))
  (ISRADIXMARKER LAMBDA (C)
    (CONTAINS (TOLOWER C) (QUOTE (98 100 111 120))))
  (ISEXACTMARKER LAMBDA (C)
    (OR (ISE C) (ISI C)))
  (ISDIGITBIN LAMBDA (C)
    (OR (EQ C 48) (EQ C 49)))
  (ISDIGITOCT LAMBDA (C)
    (ISINRANGE C 48 55))
  (ISDIGITDEC LAMBDA (C) (ISDIGIT C))
  (ISDIGITHEX LAMBDA (C)
    (OR (ISDIGIT C) (ISINRANGE (TOLOWER C) 97 102)))
  (DIGITVALUEBIN LAMBDA (C)
    (IF (ISDIGITBIN C) (SUB C 48) -1))
  (DIGITVALUEOCT LAMBDA (C)
    (IF (ISDIGITOCT C) (SUB C 48) -1))
  (DIGITVALUEDEC LAMBDA (C)
    (IF (ISDIGITDEC C) (SUB C 48) -1))
  (DIGITVALUEHEX LAMBDA (C)
    (IF (ISDIGITDEC C)
        (SUB C 48)
        (IF (ISINRANGE C 97 102)
            (SUB C 87)
            (IF (ISINRANGE C 65 70)
                (SUB C 55)
                -1))))
  (ISEXPONENTMARKER LAMBDA (C)
    (CONTAINS (TOLOWER C) (QUOTE (101 115 102 100 108))))
  (ISIMAGUNIT LAMBDA (C) (ISI C))
  (ISSIGN LAMBDA (C) (OR (EQ C 43) (EQ C 45)))
  (ISPLUS LAMBDA (C) (EQ C 43))
  (ISMINUS LAMBDA (C) (EQ C 45))
  (ISDQUOTE LAMBDA (C) (EQ C 34)) 
  (ISBACKQUOTE LAMBDA (C) (EQ C 96))
  (ISCOMMA LAMBDA (C) (EQ C 44))
  (ISATSIGN LAMBDA (C) (EQ C 64))
  (ISDELIM LAMBDA (C)
    (OR (ISWHITESPACE C)
        (CONTAINS C
          (QUOTE (40 41 34 59)))))
  (ISSLASH LAMBDA (C) (EQ C 47))
  (ISBACKSLASH LAMBDA (C) (EQ C 92))
  (ISINITIAL LAMBDA (C)
    (OR (ISLETTER C) (ISSPECIALINITIAL C)))
  (ISSPECIALSUBSEQUENT LAMBDA (C)
    (CONTAINS C
      (QUOTE (43 45 46))))
  (ISSUBSEQUENT LAMBDA (C)
    (OR (ISINITIAL C) (OR (ISDIGIT C) (ISSPECIALSUBSEQUENT C))))
  (SKIPWS LAMBDA NIL 
    (LET
      (IF (ISWHITESPACE C) (SKIPWS) C)
      (C GET)))
  (SCAN LAMBDA NIL
    (LET
      (IF (ISWHITESPACE C)
          (SCAN)
          (IF (ISSEMI C)
              (FIRST (SCAN) (SKIPCOMMENT))
              C))
      (C GET)))
  (SKIPCOMMENT LAMBDA NIL
    (LET
      (IF (ISNEWLINE C) (GET) (SKIPCOMMENT))
      (C GET)))
  (FIRST LAMBDA (A B) A)
