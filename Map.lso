dnl ---
ifdef(`map_lso_m4',,`define(`map_lso_m4',1)dnl

include(Pattern.lso)
include(Util.lso)

  (NEWMAP LAMBDA NIL
    MAPEMPTY)

  (MAPBALANCE LAMBDA (M)
    (LETREC
      (BALANCE (MAPTOLIST M))
      (BALANCE LAMBDA (L)
        (IF (ISNIL L)
            (NEWMAP)
            (LET
              (LET
                (LET
                  (LET
                    (MAPCONS (CAR PIVOT) (CDR PIVOT) MAPBLACK LM RM)
                    (LM BALANCE LEFT)
                    (RM BALANCE RIGHT))
                  (PIVOT CAR RIGHTWPIVOT)
                  (RIGHT CDR RIGHTWPIVOT))
                (LEFT CAR S)
                (RIGHTWPIVOT CDR S))
              (S SPLITMID L))))
      (SPLITMID LAMBDA (L)
        (SPLIT L (DIV (LENGTH L) 2)))))

  (MAPPUTALL LAMBDA (M L)
    (IF (ISNIL L)
        M
        (MAPPUTALL (MAPPUT M (CAR (CAR L)) (CDR (CAR L))) (CDR L))))

  (MAPDEPTH LAMBDA (M)
    (IF (EQ M MAPEMPTY)
        0
        (ADD 1 (MAX (MAPDEPTH (MAPLEFT M)) (MAPDEPTH (MAPRIGHT M))))))

  (MAPFINDPATTERN LAMBDA (M P)
    (FILTER (LAMBDA (X) (NEQ X (QUOTE F)))
      (MAP (LAMBDA (E) (PATTERN E P))
        (MAPTOLIST M))))

  (MAPPUT LAMBDA (M K V)
    (LETREC
      (MAPSETCOLOR (MAPPUTCC M K V) MAPBLACK)
      (MAPPUTCC LAMBDA (M K V)
        (IF (EQ M MAPEMPTY)
            (MAPCONS K V MAPRED MAPEMPTY MAPEMPTY)
            (LET
              (IF (EQ CMP 0)
                  (MAPSUBST M V)
                    (BALANCE
                      (IF (LEQ CMP 0)
                          (MAPCONS
                            (MAPKEY M)
                            (MAPVALUE M)
                            (MAPCOLOR M)
                            (MAPPUTCC (MAPLEFT M) K V)
                            (MAPRIGHT M))
                          (MAPCONS
                            (MAPKEY M)
                            (MAPVALUE M)
                            (MAPCOLOR M)
                            (MAPLEFT M)
                            (MAPPUTCC (MAPRIGHT M) K V)))))
              (CMP DEEPCMP K (MAPKEY M)))))
      (BALANCE LAMBDA (M)
        (LETREC
          (IF (EQ MATCH (QUOTE F))
              M
              (LETREC
                (MAPCONS YK YV MAPRED
                  (MAPCONS XK XV MAPBLACK A B)
                  (MAPCONS ZK ZV MAPBLACK C D))
                (A NTH MATCH 0)
                (XK NTH MATCH 1)
                (XV NTH MATCH 2)
                (B NTH MATCH 3)
                (YK NTH MATCH 4)
                (YV NTH MATCH 5)
                (C NTH MATCH 6)
                (ZK NTH MATCH 7)
                (ZV NTH MATCH 8)
                (D NTH MATCH 9)
                ))

          (MATCH .
            (LET (IF (EQ MATCHLL (QUOTE F))
                     (LET (IF (EQ MATCHLR (QUOTE F))
                              (LET (IF (EQ MATCHRL (QUOTE F))
                                       (LET (IF (EQ MATCHRR (QUOTE F))
                                                (QUOTE F)
                                                MATCHRR)

              (MATCHRR PATTERN M
                (QUOTE (BLACK _ _ _ (RED _ _ _ (RED _ _ _ _)))))
dnl -----------               a x x      b y y      c z z d
dnl -----------                (k,v)      (k,v)      (k,v)

                                         )
                                       MATCHRL)

              (MATCHRL PATTERN M
                (QUOTE (BLACK _ _ _ (RED (RED _ _ _ _) _ _ _))))
dnl -----------               a x x           b y y c  z z d
dnl -----------                (k,v)           (k,v)  (k,v)

                                )
                              MATCHLR)

              (MATCHLR PATTERN M
                (QUOTE (BLACK (RED _ _ _ (RED _ _ _ _)) _ _ _)))
dnl -----------                    a x x      b y y c   z z d
dnl -----------                     (k,v)      (k,v)   (k,v)

                       )
                     MATCHLL)

              (MATCHLL PATTERN M
                (QUOTE (BLACK (RED (RED _ _ _ _) _ _ _) _ _ _)))
dnl -----------                         a x x b  y y c  z z d
dnl -----------                          (k,v)  (k,v)  (k,v)

              ))))))

  (MAPGET LAMBDA (M K)
    (IF (EQ M MAPEMPTY)
        (QUOTE NIL)
        (LET
          (IF (EQ CMP 0)
              (MAPVALUE M)
              (IF (LEQ CMP 0)
                  (MAPGET (MAPLEFT M) K)
                  (MAPGET (MAPRIGHT M) K)))
          (CMP DEEPCMP K (MAPKEY M)))))

  (MAPTOLIST LAMBDA (M)
    (LETREC
      (MAPTOLISTCC M (QUOTE NIL))
      (MAPTOLISTCC LAMBDA (M CC)
        (IF (EQ M MAPEMPTY)
            CC
            (MAPTOLISTCC (MAPLEFT M)
              (CONS (CONS (MAPKEY M) (MAPVALUE M)) (MAPTOLISTCC (MAPRIGHT M) CC)))))))

  (MAPBLACK QUOTE BLACK)
  (MAPRED QUOTE RED)
  (MAPCOLOR LAMBDA (M)
    (FIRST M))
  (MAPLEFT LAMBDA (M)
    (SECOND M))
  (MAPKEY LAMBDA (M)
    (THIRD M))
  (MAPVALUE LAMBDA (M)
    (FOURTH M))
  (MAPRIGHT LAMBDA (M)
    (FIFTH M))
  (MAPCONS LAMBDA (K V C L R)
    (CONS5 C L K V R))
  (MAPSUBST LAMBDA (M V)
    (MAPCONS (MAPKEY M) V (MAPCOLOR M) (MAPLEFT M) (MAPRIGHT M)))
  (MAPSETCOLOR LAMBDA (M C)
    (LET
      (IF (EQ C COLOR)
          M
          (CONS C (CDR M)))
      (COLOR MAPCOLOR M)))
  (MAPEMPTY QUOTE NIL)

')dnl
