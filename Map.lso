dnl ---

include(Pattern.lso)
include(Util.lso)

  (NEWMAP LAMBDA NIL
    MAPEMPTY)

  (MAPPUT LAMBDA (M K V)
    (LETREC
      (MAPSETCOLOR (MAPPUTCC M K V) MAPBLACK)
      (MAPPUTCC LAMBDA (M K V)
        (IF (EQ M MAPEMPTY)
            (MAPCONS K V MAPRED MAPEMPTY MAPEMPTY)
            (LET
              (IF (EQ CMP 0)
                  (MAPSUBST M V)
                    (BALANCE
                      (IF (LEQ CMP 0)
                          (MAPCONS
                            (MAPKEY M)
                            (MAPVALUE M)
                            (MAPCOLOR M)
                            (MAPPUTCC (MAPLEFT M) K V)
                            (MAPRIGHT M))
                          (MAPCONS
                            (MAPKEY M)
                            (MAPVALUE M)
                            (MAPCOLOR M)
                            (MAPLEFT M)
                            (MAPPUTCC (MAPRIGHT M) K V)))))
              (CMP DEEPCMP K (MAPKEY M)))))
      (BALANCE LAMBDA (M)
        (LETREC
          (IF (EQ MATCH (QUOTE F))
              M
              (LETREC
                (MAPCONS YK YV MAPRED
                  (MAPCONS XK XV MAPBLACK A B)
                  (MAPCONS ZK ZV MAPBLACK C D))
                (A NTH MATCH 0)
                (XK NTH MATCH 1)
                (XV NTH MATCH 2)
                (B NTH MATCH 3)
                (YK NTH MATCH 4)
                (YV NTH MATCH 5)
                (C NTH MATCH 6)
                (ZK NTH MATCH 7)
                (ZV NTH MATCH 8)
                (D NTH MATCH 9)
                ))

          (MATCH .
            (LET (IF (EQ MATCHLL (QUOTE F))
                     (LET (IF (EQ MATCHLR (QUOTE F))
                              (LET (IF (EQ MATCHRL (QUOTE F))
                                       (LET (IF (EQ MATCHRR (QUOTE F))
                                                (QUOTE F)
                                                MATCHRR)
                
              (MATCHRR PATTERN M
                (QUOTE (BLACK VAR VAR VAR (RED VAR VAR VAR (RED VAR VAR VAR VAR)))))
dnl -----------               a   x.k x.v      b   y.k y.v      c   z.k z.v d

                                         )
                                       MATCHRL)

              (MATCHRL PATTERN M
                (QUOTE (BLACK VAR VAR VAR (RED (RED VAR VAR VAR VAR) VAR VAR VAR))))
dnl -----------               a   x.k x.v           b   y.k y.v c    z.k z.v d

                                )
                              MATCHLR)

              (MATCHLR PATTERN M
                (QUOTE (BLACK (RED VAR VAR VAR (RED VAR VAR VAR VAR)) VAR VAR VAR)))
dnl -----------                    a   x.k x.v      b   y.k y.v c     z.k z.v d

                       )
                     MATCHLL)
                
              (MATCHLL PATTERN M
                (QUOTE (BLACK (RED (RED VAR VAR VAR VAR) VAR VAR VAR) VAR VAR VAR)))
dnl -----------                         a   x.k x.v b    y.k y.v c    z.k z.v d

              ))))))

  (MAPGET LAMBDA (M K)
    (IF (EQ M MAPEMPTY)
        (QUOTE NIL)
		(LET
          (IF (EQ CMP 0)
              (MAPVALUE M)
              (IF (LEQ CMP 0)
                  (MAPGET (MAPLEFT M) K)
                  (MAPGET (MAPRIGHT M) K)))
		  (CMP DEEPCMP K (MAPKEY M)))))

  (MAPBLACK QUOTE BLACK)
  (MAPRED QUOTE RED)
  (MAPCOLOR LAMBDA (M)
    (CAR M))
  (MAPLEFT LAMBDA (M)
    (CAR (CDR M)))
  (MAPKEY LAMBDA (M)
    (CAR (CDR (CDR M))))
  (MAPVALUE LAMBDA (M)
    (CAR (CDR (CDR (CDR M)))))
  (MAPRIGHT LAMBDA (M)
    (CAR (CDR (CDR (CDR (CDR M))))))
  (MAPCONS LAMBDA (K V C L R)
    (CONS C (CONS L (CONS K (CONS V (CONS R (QUOTE NIL)))))))
  (MAPSUBST LAMBDA (M V)
    (MAPCONS (MAPKEY M) V (MAPCOLOR M) (MAPLEFT M) (MAPRIGHT M)))
  (MAPSETCOLOR LAMBDA (M C)
    (LET
      (IF (EQ C COLOR)
          M
          (CONS C (CDR M)))
      (COLOR MAPCOLOR M)))
  (MAPEMPTY QUOTE NIL)

